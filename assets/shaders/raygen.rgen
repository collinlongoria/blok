#version 460
#extension GL_EXT_ray_tracing : require

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 3, set = 0) uniform FrameUBO {
    mat4 view;
    mat4 proj;
    vec3 camPos;
    float delta_time;
} frame;
layout(binding = 4, set = 0, rgba32f) uniform image2D outputImage;

layout(location = 0) rayPayloadEXT vec3 hitColor;

void main() {
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;

    // Compute ray direction in world space
    mat4 invView = inverse(frame.view);
    mat4 invProj = inverse(frame.proj);

    vec4 target = invProj * vec4(d.x, d.y, 1.0, 1.0);
    vec3 rayDir = normalize((invView * vec4(normalize(target.xyz), 0.0)).xyz);
    vec3 rayOrigin = frame.camPos;

    float tMin = 0.001;
    float tMax = 10000.0;

    // Initialize payload
    hitColor = vec3(0.0);

    // Trace primary ray
    traceRayEXT(
        topLevelAS,           // acceleration structure
        gl_RayFlagsOpaqueEXT, // ray flags
        0xFF,                 // cull mask
        0,                    // sbt record offset
        0,                    // sbt record stride
        0,                    // miss index
        rayOrigin,            // ray origin
        tMin,                 // ray min range
        rayDir,               // ray direction
        tMax,                 // ray max range
        0                     // payload location
    );

    // Write result to output image
    imageStore(outputImage, ivec2(gl_LaunchIDEXT.xy), vec4(hitColor, 1.0));
}