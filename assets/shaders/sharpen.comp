/*
* File: sharpen.comp
* Project: blok
* Author: Collin Longoria
* Created on: 12/8/2025
*/

#version 460

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(binding = 0) uniform sampler2D inputImage;
layout(binding = 1, rgba8) uniform image2D outputImage;

layout(push_constant) uniform PushConstants {
    float sharpenStrength;
    float padding[3];
} pc;

void main() {
    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 outputSize = imageSize(outputImage);

    // bounds check
    if (pixelCoord.x >= outputSize.x || pixelCoord.y >= outputSize.y) {
        return;
    }

    vec2 texelSize = 1.0 / vec2(outputSize);
    vec2 uv = (vec2(pixelCoord) + 0.5) / vec2(outputSize);

    // 3x3 neighborhood
    // a b c
    // d e f
    // g h i

    // top row
    vec3 a = texture(inputImage, uv + texelSize * vec2(-1, -1)).rgb;
    vec3 b = texture(inputImage, uv + texelSize * vec2( 0, -1)).rgb;
    vec3 c = texture(inputImage, uv + texelSize * vec2( 1, -1)).rgb;

    // middle row
    vec3 d = texture(inputImage, uv + texelSize * vec2(-1,  0)).rgb;
    vec3 e = texture(inputImage, uv).rgb;
    vec3 f = texture(inputImage, uv + texelSize * vec2( 1,  0)).rgb;

    // bottom row
    vec3 g = texture(inputImage, uv + texelSize * vec2(-1,  1)).rgb;
    vec3 h = texture(inputImage, uv + texelSize * vec2( 0,  1)).rgb;
    vec3 i = texture(inputImage, uv + texelSize * vec2( 1,  1)).rgb;

    // gaussian blur estimation
    // weights: corners = 1, cross = 2, center = 4. total = 16

    vec3 blur = (
        1.0 * (a + c + g + i) + // corners
        2.0 * (b + d + f + h) + // cross
        4.0 * e                 // center
    ) / 16.0;

    // calculate detail
    vec3 detail = e - blur;

    // apply sharpening
    float intensity = pc.sharpenStrength * 3.0;

    vec3 result = e + detail * intensity;

    // clamp to avoid artifacts
    result = clamp(result, 0.0, 1.0);

    // output
    imageStore(outputImage, pixelCoord, vec4(result, 1.0));
}
